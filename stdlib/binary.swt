data Bit {
    "b0",
    "b1",
}

// TODO: general list type? data BinaryNumber [List Bit]
data BinaryNumber {
    "nil",
    (Bit BinaryNumber),
}

// assumes numbers are normalized (no leading zeros)
fn binary-eql?: (BinaryNumber BinaryNumber) -> {
    "false",
    "true",
} {
    ("nil" "nil") -> "true";
    (a "nil") -> "false";
    ("nil" b) -> "false";
    (("b0" a) ("b0" b)) -> "true";
    (("b1" a) ("b1" b)) -> "true";
    else -> "false";
}

fn binary-inc: BinaryNumber -> BinaryNumber {
    "nil" -> ("b1" "nil");
    ("b0" rest) -> ("b1" rest);
    ("b1" rest) -> binary-inc: rest {
        new_rest -> ("b0" new_rest);
    }
}

fn binary-add: (BinaryNumber BinaryNumber) -> BinaryNumber {
    ("nil" b) -> b;
    (a "nil") -> a;
    (("b0" a_rest) (b_first b_rest)) -> binary-add: (a_rest b_rest) {
        rest -> (b_first rest);
    }
    ((a_first a_rest) ("b0" b_rest)) -> binary-add: (a_rest b_rest) {
        rest -> (a_first rest);
    }
    (("b1" a_rest) ("b1" b_rest)) -> binary-add: (a_rest b_rest) {
        rest -> binary-inc: rest {
            actual_rest -> ("b0" actual_rest);
        }
    }
}

fn binary-times-2: BinaryNumber -> BinaryNumber {
    "nil" -> "nil";
    x -> ("b0" x);
}

fn binary-times-10: BinaryNumber -> BinaryNumber {
    x1 -> binary-times-2: x1 {
        x2 -> binary-times-2: x2 {
            x4 -> binary-times-2: x4 {
                x8 -> binary-add: (x2 x8);
            }
        }
    }
}

fn binary-multiply: (BinaryNumber BinaryNumber) -> BinaryNumber {
    (a "nil") -> "nil";
    (a ("b0" rest)) -> binary-multiply: (a rest) {
        r -> ("b0" r);
    }
    (a ("b1" rest)) -> binary-multiply: (a rest) {
        r -> binary-add: (a ("b0" r));
    }
}

data DecimalDigit {
    "0", "1", "2", "3", "4",
    "5", "6", "7", "8", "9",
}

data DecimalNumber {
    "nil",
    (DecimalDigit DecimalNumber),
}

fn binary-from-decimal: DecimalNumber -> BinaryNumber {
    "nil" -> "nil";
    (first rest) -> binary-from-decimal-digit: first {
        cur -> binary-from-decimal: rest {
            r1 -> binary-times-10: r1 {
                r2 -> binary-add: (cur r2);
            }
        }
    }
}

fn binary-from-decimal-digit: DecimalDigit -> BinaryNumber {
    "0" -> "nil";
    "1" -> ("b1" "nil");
    "2" -> ("b0" ("b1" "nil"));
    "3" -> ("b1" ("b1" "nil"));
    "4" -> ("b0" ("b0" ("b1" "nil")));
    "5" -> ("b1" ("b0" ("b1" "nil")));
    "6" -> ("b0" ("b1" ("b1" "nil")));
    "7" -> ("b1" ("b1" ("b1" "nil")));
    "8" -> ("b0" ("b0" ("b0" ("b1" "nil"))));
    "9" -> ("b1" ("b0" ("b0" ("b1" "nil"))));
}

fn decimal-from-binary: BinaryNumber -> DecimalNumber {
    "nil" -> "nil";
    (bit rest) -> decimal-from-binary: rest {
        result -> decimal-duplicate: result {
            doubled -> bit {
                "b0" -> doubled;
                "b1" -> decimal-increment: doubled;
            }
        }
    }
}

fn decimal-increment: DecimalNumber -> DecimalNumber {
    "nil" -> ("1" "nil");
    (digit rest) -> digit {
        "0" -> ("1" rest);
        "1" -> ("2" rest);
        "2" -> ("3" rest);
        "3" -> ("4" rest);
        "4" -> ("5" rest);
        "5" -> ("6" rest);
        "6" -> ("7" rest);
        "7" -> ("8" rest);
        "8" -> ("9" rest);
        "9" -> decimal-increment: rest {
            new_rest -> ("0" new_rest);
        }
    }
}

fn decimal-duplicate: DecimalNumber -> DecimalNumber {
    "nil" -> "nil";
    (digit rest) -> digit {
        "0" -> decimal-duplicate: rest {
            doubled_rest -> ("0" doubled_rest);
        }
        "1" -> decimal-duplicate: rest {
            doubled_rest -> ("2" doubled_rest);
        }
        "2" -> decimal-duplicate: rest {
            doubled_rest -> ("4" doubled_rest);
        }
        "3" -> decimal-duplicate: rest {
            doubled_rest -> ("6" doubled_rest);
        }
        "4" -> decimal-duplicate: rest {
            doubled_rest -> ("8" doubled_rest);
        }
        "5" -> decimal-duplicate: rest {
            doubled_rest -> decimal-increment: doubled_rest {
                incremented_rest -> ("0" incremented_rest);
            }
        }
        "6" -> decimal-duplicate: rest {
            doubled_rest -> decimal-increment: doubled_rest {
                incremented_rest -> ("2" incremented_rest);
            }
        }
        "7" -> decimal-duplicate: rest {
            doubled_rest -> decimal-increment: doubled_rest {
                incremented_rest -> ("4" incremented_rest);
            }
        }
        "8" -> decimal-duplicate: rest {
            doubled_rest -> decimal-increment: doubled_rest {
                incremented_rest -> ("6" incremented_rest);
            }
        }
        "9" -> decimal-duplicate: rest {
            doubled_rest -> decimal-increment: doubled_rest {
                incremented_rest -> ("8" incremented_rest);
            }
        }
    }
}

data ListOfBinaryNumbers {
    "nil",
    (BinaryNumber ListOfBinaryNumbers),
}

// first child has equal or less elements than the second one
data HeapOfBinaryNumbers {
    "nil",
    (BinaryNumber HeapOfBinaryNumbers HeapOfBinaryNumbers),
}

fn heapify: ListOfBinaryNumbers -> HeapOfBinaryNumbers {
    // [0 1 1 2 2 2 2 3 3 3 3 3 3 3 3]
    i -> heapify-helper: (i "nil");
}

fn heapify-helper: (ListOfBinaryNumbers HeapOfBinaryNumbers) -> HeapOfBinaryNumbers {
    ("nil" h) -> h;
    ((f rest) h) -> heapify-siftdown: (f h) {
        h2 -> heapify-helper: (rest h2);
    }
}

fn heapify-siftdown: (BinaryNumber HeapOfBinaryNumbers) -> HeapOfBinaryNumbers {
    (n "nil") -> (n "nil" "nil");
    (n (r x y)) -> binary-compare: (n r) {
        "second_is_greater" -> heapify-siftdown: (n x) {
            x2 -> (r y x2);
        }
        else -> heapify-siftdown: (r x) {
            x2 -> (n y x2);
        }
    }
}

fn heapify-popmax: HeapOfBinaryNumbers -> {
    "nil",
    (BinaryNumber HeapOfBinaryNumbers),
} {
    h -> heapify-popmax-incorrect: h;
}

// the returned heap is possibly unbalanced!
fn heapify-popmax-incorrect: HeapOfBinaryNumbers -> {
    "nil",
    (BinaryNumber HeapOfBinaryNumbers),
} {
    "nil" -> "nil";
    (r x "nil") -> (r x);
    (r "nil" y) -> (r y);
    (r (xr xx xy) (yr yx yy)) -> binary-compare: (xr yr) {
        "second_is_greater" -> heapify-popmax-incorrect: (yr yx yy) {
            (nr y2) -> (r (nr (xr xx xy) y2));
        }
        else -> heapify-popmax-incorrect: (xr xx xy) {
            (nr x2) -> (r (nr x2 (yr yx yy)));
        }
    }
}

fn heapsort-repeatedly-pop: HeapOfBinaryNumbers -> ListOfBinaryNumbers {
    h -> heapify-popmax: h {
        "nil" -> "nil";
        (v rh) -> heapsort-repeatedly-pop: rh {
            r -> (v r);
        }
    }
}

fn heapsort-list-of-binary-numbers: ListOfBinaryNumbers -> ListOfBinaryNumbers {
    i -> heapify: i {
        h -> heapsort-repeatedly-pop: h;
    }
}

fn sort-list-of-binary-numbers: ListOfBinaryNumbers -> ListOfBinaryNumbers {
    // l -> insertionsort-list-of-binary-numbers: l;
    l -> heapsort-list-of-binary-numbers: l;
}

fn insertionsort-list-of-binary-numbers: ListOfBinaryNumbers -> ListOfBinaryNumbers {
    "nil" -> "nil";
    (first rest) -> sort-list-of-binary-numbers: rest {
        sorted_rest -> _sort-helper: (first sorted_rest);
    }
}

fn _sort-helper: (BinaryNumber ListOfBinaryNumbers) -> ListOfBinaryNumbers {
    (x "nil") -> (x "nil");
    (x (first rest)) -> binary-compare: (x first) {
        "first_is_greater" -> _sort-helper: (x rest) {
            done -> (first done);
        }
        other -> (x (first rest));
    }
}

fn binary-compare: (BinaryNumber BinaryNumber) -> {
    "first_is_greater",
    "equal",
    "second_is_greater",
} {
    ("nil" "nil") -> "equal";
    ("nil" b) -> "second_is_greater";
    (a "nil") -> "first_is_greater";
    ((a_bit a_rest) (b_bit b_rest)) -> binary-compare: (a_rest b_rest) {
        "equal" -> (a_bit b_bit) {
            ("b0" "b0") -> "equal";
            ("b1" "b1") -> "equal";
            ("b0" "b1") -> "second_is_greater";
            ("b1" "b0") -> "first_is_greater";
        }
        other -> other;
    }
}

data Sign {
    "positive",
    "negative",
}

// Helper function to perform subtraction assuming a >= b
fn binary-subtract-with-nonnegative-result: (BinaryNumber BinaryNumber) -> BinaryNumber {
    (a "nil") -> a;
    // ("nil" b) -> unreachable;
    (("b0" a_rest) ("b0" b_rest)) -> binary-subtract-with-nonnegative-result: (a_rest b_rest) {
        rest -> binary-normalize: ("b0" rest);
    }
    (("b1" a_rest) ("b1" b_rest)) -> binary-subtract-with-nonnegative-result: (a_rest b_rest) {
        rest -> binary-normalize: ("b0" rest);
    }
    (("b1" a_rest) ("b0" b_rest)) -> binary-subtract-with-nonnegative-result: (a_rest b_rest) {
        rest -> binary-normalize: ("b1" rest);
    }
    (("b0" a_rest) ("b1" b_rest)) -> binary-subtract-with-nonnegative-result-helper-borrow: (a_rest b_rest) {
        rest -> binary-normalize: ("b1" rest);
    }
}

fn binary-subtract-with-nonnegative-result-helper-borrow: (BinaryNumber BinaryNumber) -> BinaryNumber {
    (a "nil") -> binary-dec: a;
    // ("nil" b) -> unreachable;
    (("b0" a_rest) ("b0" b_rest)) -> binary-subtract-with-nonnegative-result-helper-borrow: (a_rest b_rest) {
        rest -> ("b1" rest);
    }
    (("b1" a_rest) ("b1" b_rest)) -> binary-subtract-with-nonnegative-result-helper-borrow: (a_rest b_rest) {
        rest -> ("b1" rest);
    }
    (("b1" a_rest) ("b0" b_rest)) -> binary-subtract-with-nonnegative-result: (a_rest b_rest) {
        rest -> ("b0" rest);
    }
    (("b0" a_rest) ("b1" b_rest)) -> binary-subtract-with-nonnegative-result-helper-borrow: (a_rest b_rest) {
        rest -> ("b0" rest);
    }
}

// Helper function to decrement a binary number (opposite of binary-inc)
fn binary-dec: BinaryNumber -> BinaryNumber {
    ("b1" rest) -> ("b0" rest);
    ("b0" rest) -> binary-dec: rest {
        new_rest -> ("b1" new_rest);
    }
}

// Helper function to remove leading zeros
fn binary-normalize: BinaryNumber -> BinaryNumber {
    "nil" -> "nil";
    ("b0" rest) -> binary-normalize: rest {
        "nil" -> "nil";
        other -> ("b0" other);
    }
    ("b1" rest) -> ("b1" rest);
}

// Main subtraction function
fn binary-subtract: (BinaryNumber BinaryNumber) -> (Sign BinaryNumber) {
    (a b) -> binary-compare: (a b) {
        "equal" -> ("positive" "nil");
        "first_is_greater" -> binary-subtract-with-nonnegative-result: (a b) {
            result -> ("positive" result);
        }
        "second_is_greater" -> binary-subtract-with-nonnegative-result: (b a) {
            result -> ("negative" result);
        }
    }
}
